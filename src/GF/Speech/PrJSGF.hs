----------------------------------------------------------------------
-- |
-- Module      : PrJSGF
-- Maintainer  : BB
-- Stability   : (stable)
-- Portability : (portable)
--
-- > CVS $Date: 2005/03/21 14:17:44 $ 
-- > CVS $Author: peb $
-- > CVS $Revision: 1.7 $
--
-- This module prints a CFG as a JSGF grammar.
--
-- FIXME: remove \/ warn \/ fail if there are int \/ string literal
-- categories in the grammar
--
-- FIXME: convert to UTF-8
-----------------------------------------------------------------------------

module PrJSGF (jsgfPrinter) where

import SRG
import Ident
import GF.Conversion.CFGrammar
import GF.Parsing.Parser (Symbol(..))
import GF.Conversion.GrammarTypes
import GF.Printing.PrintParser
import Option

jsgfPrinter :: Ident -- ^ Grammar name
	   -> Options -> CFGrammar -> String
jsgfPrinter name opts cfg = prJSGF srg ""
    where srg = makeSRG name opts cfg

prJSGF :: SRG -> ShowS
prJSGF (SRG{grammarName=name,startCat=start,origStartCat=origStart,rules=rs})
    = header . mainCat . unlinesS (map prRule rs)
    where
    header = showString "#JSGF V1.0 UTF-8;" . nl 
	     . comments ["JSGF speech recognition grammar for " ++ name,
			 "Generated by GF"] . nl 
	     . showString ("grammar " ++ name ++ ";") . nl
	     . nl
    mainCat = comments ["Start category: " ++ origStart] . nl 
	      . showString "public <MAIN> = " . prCat start . showChar ';' . nl . nl
    prRule (SRGRule cat origCat rhs) = 
	comments [origCat] . nl
        . prCat cat . showString " = " . join " | " (map prAlt rhs) . nl
    prAlt rhs | null rhs' = showString "<NULL>"
	      | otherwise = wrap "(" (unwordsS (map prSymbol rhs')) ")"
		   where rhs' = rmPunct rhs
    prSymbol (Cat c) = prCat c
    prSymbol (Tok t) = wrap "\"" (prtS t) "\""
    prCat c = showChar '<' . showString c . showChar '>'

rmPunct :: [Symbol String Tokn] -> [Symbol String Tokn] 
rmPunct [] = []
rmPunct (Tok t:ss) | all isPunct (prt t) = rmPunct ss
rmPunct (s:ss) = s : rmPunct ss

isPunct :: Char -> Bool
isPunct c = c `elem` "-_.;.,?!"

comments :: [String] -> ShowS
comments = unlinesS . map (showString . ("// " ++))
