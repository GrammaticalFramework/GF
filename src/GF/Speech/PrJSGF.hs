----------------------------------------------------------------------
-- |
-- Module      : PrJSGF
-- Maintainer  : BB
-- Stability   : (stable)
-- Portability : (portable)
--
-- > CVS $Date: 2005/09/07 14:21:30 $ 
-- > CVS $Author: bringert $
-- > CVS $Revision: 1.14 $
--
-- This module prints a CFG as a JSGF grammar.
--
-- FIXME: remove \/ warn \/ fail if there are int \/ string literal
-- categories in the grammar
--
-- FIXME: convert to UTF-8
-----------------------------------------------------------------------------

module GF.Speech.PrJSGF (jsgfPrinter) where

import GF.Speech.SRG
import GF.Infra.Ident
import GF.Formalism.CFG
import GF.Formalism.Utilities (Symbol(..))
import GF.Conversion.Types
import GF.Infra.Print
import GF.Infra.Option

jsgfPrinter :: Ident -- ^ Grammar name
	   -> Options -> CGrammar -> String
jsgfPrinter name opts cfg = prJSGF srg ""
    where srg = makeSRG name opts cfg

prJSGF :: SRG -> ShowS
prJSGF (SRG{grammarName=name,startCat=start,origStartCat=origStart,rules=rs})
    = header . mainCat . unlinesS (map prRule rs)
    where
    header = showString "#JSGF V1.0 UTF-8;" . nl 
	     . comments ["JSGF speech recognition grammar for " ++ name,
			 "Generated by GF"] . nl 
	     . showString ("grammar " ++ name ++ ";") . nl
	     . nl
    mainCat = comments ["Start category: " ++ origStart] . nl 
	      . showString "public <MAIN> = " . prCat start . showChar ';' . nl . nl
    prRule (SRGRule cat origCat rhs) = 
	comments [origCat] . nl
        . prCat cat . showString " = " . join " | " (map prAlt rhs) . nl
    prAlt rhs | null rhs' = showString "<NULL>"
	      | otherwise = wrap "(" (unwordsS (map prSymbol rhs')) ")"
		   where rhs' = rmPunct rhs
    prSymbol (Cat c) = prCat c
    prSymbol (Tok t) = wrap "\"" (prtS t) "\""
    prCat c = showChar '<' . showString c . showChar '>'

rmPunct :: [Symbol String Token] -> [Symbol String Token] 
rmPunct [] = []
rmPunct (Tok t:ss) | all isPunct (prt t) = rmPunct ss
rmPunct (s:ss) = s : rmPunct ss

isPunct :: Char -> Bool
isPunct c = c `elem` "-_.;.,?!"

comments :: [String] -> ShowS
comments = unlinesS . map (showString . ("// " ++))
